# Maintainer: Luca Gasperini <info at xsoftware dot eu>

pkgname=falkon-xs
_gitname=falkon
pkgver=5093.8c9ce314
pkgrel=1
pkgdesc="Cross-platform Qt Web Browser"
arch=('i686' 'x86_64')
url="http://www.qupzilla.com"
license=('GPL')
depends=('qt5-webengine' 'qt5-x11extras' 'qt5-svg' 'hicolor-icon-theme' 'openssl>=1.1.0')
makedepends=('git' 'qt5-tools' 'kwallet' 'libgnome-keyring' 'extra-cmake-modules')
conflicts=('qupzilla' 'qupzilla-qt5' 'qupzilla-qt5-git' 'falkon')
optdepends=(
  'bash-completion: bash completion support'
  'kwallet: kf5 kwallet integration'
  'libgnome-keyring: gnome keyring integration'
  )
source=("git+https://github.com/Nobel3D/falkon.git#branch=xsoftware")
md5sums=('SKIP')

pkgver() {
	cd $_gitname
	echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
	cd $_gitname
	cmake \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DKDE_INTEGRATION=ON \
		-DGNOME_INTEGRATION=ON \
		-DKDE_INSTALL_LIBDIR=lib \
		-DCMAKE_BUILD_TYPE=Release \
		-DDISABLE_UPDATES_CHECK=ON
  make
}

package() {
	cd $_gitname
	make DESTDIR="${pkgdir}/" install

	# zsh completion
	install -Dm644 $srcdir/$_gitname/linux/completion/_falkon "$pkgdir/usr/share/zsh/site-functions/_falkon"
}
